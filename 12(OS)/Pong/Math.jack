class Math {
    //This variable is used as a flag for divisionOverFlow
    static boolean divisionOverFlow;
    static Array twoToThe;

    function void init() {
        //Initialise this variable by assigning false to it
        let divisionOverFlow = false;

        let twoToThe = Memory.alloc(16);

        let twoToThe[0]  = 1;
        let twoToThe[1]  = 2;
        let twoToThe[2]  = 4;
        let twoToThe[3]  = 8;
        let twoToThe[4]  = 16;
        let twoToThe[5]  = 32;
        let twoToThe[6]  = 64;
        let twoToThe[7]  = 128;
        let twoToThe[8]  = 256;
        let twoToThe[9]  = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = -32767-1;

        return;
    }

    function int twoToThe(int i) {
        return twoToThe[i];
    }

    function int multiply(int x, int y) {

        var int sum, xShifted, i, placeValue, bitCheck;

        let sum = 0;
        let xShifted = x;
        let i = 0; //This used as counter or incrementer

        while (~(i > 15)) {
            //In placeValue only 1 bit is 1 and others are zero that one bit is ith indexed, 
            //if y also have one at that position we will add xShifted to the sum
            let bitCheck = (y & twoToThe[i]); 
            if (~(bitCheck = 0)) {
                let sum = sum + xShifted;
            }
            let xShifted = xShifted + xShifted; //This works as left shift.Refer to long division method.
            let i = i + 1;
        }

        return sum;
    }

    function int divide(int x, int y) {
        
        var int q, temp, flag, result;

        let flag = ((x < 0) = (y < 0));

        let x = Math.abs(x);
        let y = Math.abs(y);

        //We have to exit from the function by returning 0 if there is divisionOverFlow or y > x (divisor > dividend)
        if (y > x | divisionOverFlow) {
            //This is crucial if there is divisionOverFlow then that variable is assigned true.
            //And that is static variable so we must assing false to that variabl before returing,
            //else the divisionOverFlow variable will have true always once it gets assigned with true value.
            let divisionOverFlow = false;
            return 0;
        }

        let temp = y + y;
        let divisionOverFlow = true & (temp < 0); //If the positive num becomes negative by addition then it is overflown

        let q = Math.divide(x, temp);

        //Result is stored in result variable ,used only addition operations for efficiency
        if ((x - ((q + q)*y)) < y) {
            let result = q + q;
        }
        else {
            let result = q + q + 1;
        }

        if (flag) {return result;}
        return -result;
    }

    function int sqrt(int x) {
        //y^2 <= x < (y + 1)^2 for 0 <= x < 2^n
        //i is used as incrementer or counter

        var int y, i, mid, temp1, temp2; 

        let mid = 8;
        let y = 0;
        let i = mid - 1;

        while (~(i < 0)) {
            let temp1 = y + Math.twoToThe(i);
            let temp2 = temp1 * temp1;
            //If temp is negative then it is overflown.
            // temp2 > 0 will check for overFlow issue
            if (~(temp2 > x) & (temp2 > 0)) {
                let y = temp1;
            }
            let i = i - 1;
        }
        return y;
    }

    function int abs(int x) {
        if (x < 0) {return -x;}
        return x;
    }

    function int min(int x, int y) {
        if (x < y) {return x;}
        return y;
    }

    function int max(int x, int y) {
        if (x > y) {return x;}
        return y;
    }
}